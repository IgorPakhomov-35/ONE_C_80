
Функция ИННВерен(ИНН, ТипКонтрагента, ТекстОшибки = "")Экспорт
	
	Если Не ЗначениеЗаполнено(ИНН) Тогда //Пустой ИНН верен
		Возврат Истина;
	КонецЕсли; 
	
	Если СтрДлина(ИНН) <> КорректнаяДлинаИНН(ТипКонтрагента) Тогда //Если введен не полный ИНН в зависимости типа контрагента
		ТекстОшибки = СтрШаблон("Для %1 корректная длина %2!",ТипКонтрагента,КорректнаяДлинаИНН(ТипКонтрагента));
		Возврат Ложь;
	КонецЕсли;
	
	Если НЕ ТолькоЦифры(ИНН) Тогда //Проверка ИНН на наличие не цифр
		ТекстОшибки = "В ИНН должны быть только цифры!";
		Возврат Ложь;
	КонецЕсли;
		
	Если СтрДлина(ИНН) = 10 Тогда
		ИННЮрЛицаВерен(ИНН); 
		Если НЕ ИННЮрЛицаВерен(ИНН) Тогда
			ТекстОшибки = "ИНН не прошел проверку на контрольный разряд!";
			Возврат Ложь;
		КонецЕсли;
	ИначеЕсли СтрДлина(ИНН) = 12 Тогда	
		ИННФизЛицаВерен(ИНН);
		Если НЕ ИННФизЛицаВерен(ИНН) Тогда	
			ТекстОшибки = "ИНН не прошел проверку на контрольный разряд!";
			Возврат Ложь;	
			КонецЕсли;
		КонецЕсли;
		
	Возврат Истина;
	
КонецФункции

Функция ТолькоЦифры(Знач ИНН) 
	
	Перем НомерСимвола, СимволИНН, ТолькоЦифры, Цифры;
	
	ТолькоЦифры = Истина; 
	Цифры = "0123456789";
	Для НомерСимвола = 1 По СтрДлина(ИНН) Цикл
		
		СимволИНН = Сред(ИНН, НомерСимвола, 1);
		
		Если СтрНайти(Цифры,СимволИНН) = 0 Тогда
			ТолькоЦифры = Ложь;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	Возврат ТолькоЦифры;

КонецФункции

Функция КорректнаяДлинаИНН(Знач ТипКонтрагента) //В зависимости от выбора на форме предопределенного значения определяем длину ИНН
	
	Перем КорректнаяДлина;
	
	Если ТипКонтрагента = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ЮридическоеЛицо") Тогда
		КорректнаяДлина = 10;
	Иначе	
		КорректнаяДлина = 12;
	КонецЕсли;
	
	Возврат КорректнаяДлина;
	
КонецФункции

Функция ИННЮрЛицаВерен(ИНН)
	
	ИННМассив = Новый Массив; 
	
	Для Счетчик = 1 По 10 Цикл
		СимволИНН = Сред(ИНН,Счетчик, 1);
		ИННЧисло = Число(СимволИНН);
		ИННМассив.Добавить(ИННЧисло);
	КонецЦикла;
	
	КонтрольныйСимволИНН = ИННМассив.Получить(9); 
	
	КонтрольнаяСумма = ИННМассив.Получить(0) * 2 + ИННМассив.Получить(1) * 4 + ИННМассив.Получить(2) * 10 + 
	ИННМассив.Получить(3) * 3 + ИННМассив.Получить(4) * 5 + ИННМассив.Получить(5) * 9
	+ ИННМассив.Получить(6) * 4 + ИННМассив.Получить(7) * 6 + ИННМассив.Получить(8) * 8;
	
	КонтрольныйСимволРасчетный = КонтрольнаяСумма % 11;
	Если КонтрольныйСимволРасчетный = 10 Тогда
		КонтрольныйСимволРасчетный = 0
	КонецЕсли;
	Если КонтрольныйСимволИНН = КонтрольныйСимволРасчетный Тогда
		Возврат Истина;
	Иначе 
		Возврат Ложь;
		
	КонецЕсли;	 
	
КонецФункции 

Функция ИННФизЛицаВерен(ИНН)
	
	ИННМассив = Новый Массив; 
	
	Для Счетчик = 1 По 12 Цикл
		СимволИНН = Сред(ИНН,Счетчик, 1);
		ИННЧисло = Число(СимволИНН);
		ИННМассив.Добавить(ИННЧисло);
	КонецЦикла;
	
	КонтрольныйСимволИНН11 = ИННМассив.Получить(10);
	КонтрольныйСимволИНН12 = ИННМассив.Получить(11);
	
	КонтрольнаяСумма11 = ИННМассив.Получить(0) * 7 + ИННМассив.Получить(1) * 2 + ИННМассив.Получить(2) * 4 + 
	ИННМассив.Получить(3) * 10 + ИННМассив.Получить(4) * 3 + ИННМассив.Получить(5) * 5
	+ ИННМассив.Получить(6) * 9 + ИННМассив.Получить(7) * 4 + ИННМассив.Получить(8) * 6 + ИННМассив.Получить(9) * 8;  
	
	КонтрольнаяСумма12 = ИННМассив.Получить(0) * 3 + ИННМассив.Получить(1) * 7 + ИННМассив.Получить(2) * 2 + 
	ИННМассив.Получить(3) * 4 + ИННМассив.Получить(4) * 10 + ИННМассив.Получить(5) * 3
	+ ИННМассив.Получить(6) * 5 + ИННМассив.Получить(7) * 9 + ИННМассив.Получить(8) * 4 + ИННМассив.Получить(9) * 6 + ИННМассив.Получить(10) * 8;
	
	КонтрольныйСимволРасчетный11 = КонтрольнаяСумма11 % 11;
	Если КонтрольныйСимволРасчетный11 = 10 Тогда
		КонтрольныйСимволРасчетный11 = 0
	КонецЕсли; 
	
	КонтрольныйСимволРасчетный12 = КонтрольнаяСумма12 % 11;
	Если КонтрольныйСимволРасчетный12 = 10 Тогда
		КонтрольныйСимволРасчетный12 = 0
	КонецЕсли;
	
	Если КонтрольныйСимволИНН11 = КонтрольныйСимволРасчетный11 И КонтрольныйСимволИНН12 = КонтрольныйСимволРасчетный12 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
		
	КонецЕсли;	 
	
КонецФункции 
