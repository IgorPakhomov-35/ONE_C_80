

&НаКлиенте
Процедура ЦеныНоменклатураПриИзменении(Элемент)
	
	ИзмененнаяСтрока = Элементы.Цены.ТекущиеДанные;
	ПриИзмененииНоменклатуры(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНоменклатуры(ИзмененнаяСтрока)
	
	Дата = Объект.Дата;
	Если ЗначениеЗаполнено(ИзмененнаяСтрока.Номенклатура) Тогда
		ИзмененнаяСтрока.Цена = ЦеныВызовСервера.ЦенаНаДату(ИзмененнаяСтрока.Номенклатура,Дата);
	Иначе
		ИзмененнаяСтрока.Цена = 0;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",ПараметрыФормы,Элементы.Цены);
КонецПроцедуры

&НаКлиенте
Процедура ЦеныОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Номенклатура", ВыбранноеЗначение);
	
	СтрокиТаблицы = Объект.Цены.НайтиСтроки(ОтборСтрок);
	Если СтрокиТаблицы.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Объект.Цены.Добавить();
	НоваяСтрока.Номенклатура = ВыбранноеЗначение;
	ПриИзмененииНоменклатуры(НоваяСтрока);
	
КонецПроцедуры




