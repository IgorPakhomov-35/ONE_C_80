
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Зарплата.Очистить();
	
	Для каждого СтрокаСотрудник Из Сотрудники Цикл
		
		Движение = Движения.Зарплата.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.НачисленияУдержания.ПремияПроцентом;
		Движение.Сотрудник = СтрокаСотрудник.Сотрудник;
		Движение.ПериодРегистрации = Дата;
		Движение.ПериодДействияНачало = НачалоМесяца(Дата);
		Движение.ПериодДействияКонец = КонецМесяца(Дата);
		Движение.БазовыйПериодНачало = НачалоМесяца(Дата);
		Движение.БазовыйПериодКонец = КонецМесяца(Дата);
		
	КонецЦикла;
	
	Движения.Зарплата.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ЗарплатаБазаЗарплата.НомерСтроки КАК НомерСтроки,
	|	ЗарплатаБазаЗарплата.СуммаБаза КАК СуммаБаза
	|ИЗ
	|	РегистрРасчета.Зарплата.БазаЗарплата(
	|			&Измерения,
	|			&Измерения,
	|			,
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &ВидРасчета) КАК ЗарплатаБазаЗарплата";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета", ПланыВидовРасчета.НачисленияУдержания.ПремияПроцентом);
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		Движение = Движения.Зарплата[Выборка.НомерСтроки - 1];
		Сумма = Выборка.СуммаБаза * ПроцентПремии / 100;
		Движение.Сумма = Сумма;
	
	КонецЦикла;
	
	Движения.Зарплата.Записать(, Истина);
	
КонецПроцедуры
