&НаКлиенте
Процедура ТоварыИУслугиНоменклатураПриИзменении(Элемент)
	
	ИзмененнаяСтрока = Элементы.ТоварыИУслуги.ТекущиеДанные;
	ПриИзмененииНоменклатуры(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииНоменклатуры(ИзмененнаяСтрока)

	Дата = Объект.Дата;
	Если ЗначениеЗаполнено(ИзмененнаяСтрока.Номенклатура) Тогда
		ИзмененнаяСтрока.Цена = ЦеныВызовСервера.ЦенаНаДату(ИзмененнаяСтрока.Номенклатура,Дата);
		ИзмененнаяСтрока.Скидка = ЦеныВызовСервера.СкидкаНаДату(ИзмененнаяСтрока.Номенклатура,Дата);
	Иначе
		ИзмененнаяСтрока.Цена = 0;
		ИзмененнаяСтрока.Скидка = 0;
	КонецЕсли;
	
	СтавкаНДС = ПолучитьСтавкуНДС(ИзмененнаяСтрока.Номенклатура);
	ИзмененнаяСтрока.СтавкаНДС = СтавкаНДС;
	
	ПриИзмененииЦены(ИзмененнаяСтрока);
	ПриИзмененииСкидки(ИзмененнаяСтрока);
	ПриИзмененииСтавкиНДС(ИзмененнаяСтрока);

КонецПроцедуры

&НаСервере
Функция ПолучитьСтавкуНДС(Номенклатура)
		
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Номенклатура.СтавкаНДС КАК СтавкаНДС
	|ИЗ
	|	Справочник.Номенклатура КАК Номенклатура
	|ГДЕ
	|	Номенклатура.Ссылка = &Номенклатура";
	Запрос.УстановитьПараметр("Номенклатура",Номенклатура);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтавкаНДС = Выборка.СтавкаНДС;
	Иначе;
		СтавкаНДС = 0;
	КонецЕсли;
	
	Возврат СтавкаНДС;

КонецФункции

&НаКлиенте
Функция СуммаПоСтроке(Строка)
	
	Строка.Сумма = Строка.Количество * Строка.Цена * ((100 - Строка.Скидка) * 0.01);
	Возврат Строка.Сумма;
	
КонецФункции

&НаКлиенте
Процедура ПриИзмененииКоличества(ИзмененнаяСтрока)
	
	ИзмененнаяСтрока.Сумма = СуммаПоСтроке(ИзмененнаяСтрока);
	ПриИзмененииСуммы(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииЦены(ИзмененнаяСтрока)
	
	ИзмененнаяСтрока.Сумма = СуммаПоСтроке(ИзмененнаяСтрока);
	ПриИзмененииСуммы(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСкидки(ИзмененнаяСтрока)
	
	ИзмененнаяСтрока.Сумма = СуммаПоСтроке(ИзмененнаяСтрока);
	ПриИзмененииСуммы(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСуммы(ИзмененнаяСтрока)
	
	ИзмененнаяСтрока.СуммаНДС = НДСКлиентСервер.СуммаНДСПоСтавке(ИзмененнаяСтрока.Сумма, ИзмененнаяСтрока.СтавкаНДС);
	Если ИзмененнаяСтрока.Количество = 0 ИЛИ ИзмененнаяСтрока.Цена = 0 Тогда
		Возврат;
	Иначе
		ИзмененнаяСтрока.Скидка = 100 * (1 - ИзмененнаяСтрока.Сумма / ИзмененнаяСтрока.Количество / ИзмененнаяСтрока.Цена);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСтавкиНДС(ИзмененнаяСтрока)
	
	ИзмененнаяСтрока.СуммаНДС = НДСКлиентСервер.СуммаНДСПоСтавке(ИзмененнаяСтрока.Сумма, ИзмененнаяСтрока.СтавкаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиКоличествоПриИзменении(Элемент)
	
	ИзмененнаяСтрока = Элементы.ТоварыИУслуги.ТекущиеДанные;
	ПриИзмененииКоличества(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиЦенаПриИзменении(Элемент)
	
	ИзмененнаяСтрока = Элементы.ТоварыИУслуги.ТекущиеДанные;
	ПриИзмененииЦены(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСкидкаПриИзменении(Элемент)
	
	ИзмененнаяСтрока = Элементы.ТоварыИУслуги.ТекущиеДанные;
	ПриИзмененииСкидки(ИзмененнаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСуммаПриИзменении(Элемент)
	
	ИзмененнаяСтрока = Элементы.ТоварыИУслуги.ТекущиеДанные;
	ПриИзмененииСуммы(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиСтавкаНДСПриИзменении(Элемент)
	
	ИзмененнаяСтрока = Элементы.ТоварыИУслуги.ТекущиеДанные;
	ПриИзмененииСтавкиНДС(ИзмененнаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора",ПараметрыФормы,Элементы.ТоварыИУслуги);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыИУслугиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОтборСтрок = Новый Структура;
	ОтборСтрок.Вставить("Номенклатура", ВыбранноеЗначение);
	
	СтрокиТаблицы = Объект.ТоварыИУслуги.НайтиСтроки(ОтборСтрок);
	Если СтрокиТаблицы.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Объект.ТоварыИУслуги.Добавить();
	НоваяСтрока.Номенклатура = ВыбранноеЗначение;
	ПриИзмененииНоменклатуры(НоваяСтрока);
	
КонецПроцедуры









